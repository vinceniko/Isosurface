// Each #kernel tells which function to compile; you can have many kernels
#pragma kernel SphereKernel
#pragma kernel TorusKernel
#pragma kernel PyramidKernel
#pragma kernel OctahedronKernel

RWStructuredBuffer<float> _IsoVals;
uint _Resolution;
float _Step, _Time;
float4x4 _GridToWorld;
float4x4 _ShapeToWorld[2];

int threeDToOneD(int3 pos) {
	return pos.x + _Resolution * (pos.y + _Resolution * pos.z);
}

float3 GetPos(uint3 id)
{
    return mul(_GridToWorld, float4((id.zyx - _Resolution * 0.5) * _Step + _Step * 0.5, 1.0)).xyz;
    // return mul(_GridToWorld, float4(id.zyx * _Step, 1.0)).xyz;
}

void SetIsoVal(uint3 id, float val) 
{
	// id = mul(_ShapeToWorld, float4(id.zyx, 1.0)).xyz;
	if (id.x < _Resolution && id.y < _Resolution && id.z < _Resolution) 
  {
		_IsoVals[threeDToOneD(id)] = val;
	}
}

float Sphere(float3 p, float s)
{
	return length(p) - s;
}

float Torus(float3 p, float s)
{
	float2 q = float2(length(p.xz)-s * 2.5,p.y);
  	return length(q)-s;
}

float Pyramid(float3 p, float h)
{
  float m2 = h*h + 0.25;
    
  p.xz = abs(p.xz);
  p.xz = (p.z>p.x) ? p.zx : p.xz;
  p.xz -= 0.5;

  float3 q = float3( p.z, h*p.y - 0.5*p.x, h*p.x + 0.5*p.y);
   
  float s = max(-q.x,0.0);
  float t = clamp( (q.y-0.5*p.z)/(m2+0.25), 0.0, 1.0 );
    
  float a = m2*(q.x+s)*(q.x+s) + q.y*q.y;
  float b = m2*(q.x+0.5*t)*(q.x+0.5*t) + (q.y-m2*t)*(q.y-m2*t);
    
  float d2 = min(q.y,-q.x*m2-q.y*0.5) > 0.0 ? 0.0 : min(a,b);
    
  return sqrt( (d2+q.z*q.z)/m2 ) * sign(max(q.z,-p.y));
}

float Octahedron(float3 p, float s)
{
  p = abs(p);
  float m = p.x+p.y+p.z-s;
  float3 q;
       if( 3.0*p.x < m ) q = p.xyz;
  else if( 3.0*p.y < m ) q = p.yzx;
  else if( 3.0*p.z < m ) q = p.zxy;
  else return m*0.57735027;
    
  float k = clamp(0.5*(q.z-q.y+s),0.0,s); 
  return length(float3(q.x,q.y-s+k,q.z-k)); 
}

float opUnion( float d1, float d2 ) { return min(d1,d2); }

#define KERNEL_FUNCTION(function) \
	[numthreads(4, 4, 4)] \
	void function##Kernel (uint3 id: SV_DispatchThreadID) \
    { \
		float3 pos = GetPos(id); \
		SetIsoVal(id, function(mul(_ShapeToWorld[0], float4(pos, 1.0)).xyz, 0.5)); \
	}

// SetIsoVal(id, opUnion(function(mul(_ShapeToWorld[0], float4(uv, 1.0)).xyz, 0.5), function(mul(_ShapeToWorld[1], float4(uv, 1.0)).xyz, 0.5)));
// SetIsoVal(id, function(uv, _ShapeSize));
// SetIsoVal(id, function(mul(transpose(_ShapeToWorld), float4(uv, 1.0)).xyz, _ShapeSize)); 


KERNEL_FUNCTION(Sphere)
KERNEL_FUNCTION(Torus)
KERNEL_FUNCTION(Pyramid)
KERNEL_FUNCTION(Octahedron)
